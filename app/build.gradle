apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

configurations {
    ktlint
}

android {
    compileSdkVersion Versions.compile_sdk
    buildToolsVersion Versions.buildToolsVersion
    defaultConfig {
        applicationId "io.github.erikcaffrey.livedata_databinding"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionCode Versions.version_code
        versionName Versions.version_name
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    ktlint Libs.ktlint

    implementation Libs.kotlin_stdlib
    implementation Libs.appcompat
    implementation Libs.recyclerview
    implementation Libs.constraint_layout

    implementation Libs.glide
    kapt Libs.glide

    implementation Libs.lifecycle
    kapt Libs.lifecycle
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
